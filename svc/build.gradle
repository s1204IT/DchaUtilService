plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace = 'jp.co.benesse.dcha.dchautilservice'
    compileSdk = 35

    defaultConfig {
        minSdk = 22
    }

    buildFeatures {
        aidl = true
        buildConfig = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group = rootProject.POM_GROUP
            artifactId = rootProject.POM_ID
            version = rootProject.POM_VERSION
            afterEvaluate {
                from components.release
            }

            pom {
                name = rootProject.POM_NAME
                description = rootProject.POM_DESCRIPTION
                url = rootProject.POM_URL
                scm {
                    connection = rootProject.POM_SCM_CONNECTION
                    url = rootProject.POM_SCM_URL
                }
                licenses {
                    license {
                        name = rootProject.POM_LICENCE_NAME
                        url = rootProject.POM_LICENCE_URL
                    }
                }
                developers {
                    developer {
                        name = rootProject.POM_DEVELOPER_NAME
                        url = rootProject.POM_DEVELOPER_URL
                    }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
